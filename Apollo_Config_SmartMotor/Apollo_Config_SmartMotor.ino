// Application code is at bottom of this file
// Plug the Apollo into the computer, a battery into the Motor power port, and a single Smart Motor into one of the Smart Motor Ports.  
// That motor will be addressed by the broadcast ID of 254 and reset to a new ID.
// The voltage of the battery will be printed out to the Serial Terminal to verify the change.

#define GET_LOW_BYTE(A) (uint8_t)((A))
#define GET_HIGH_BYTE(A) (uint8_t)((A) >> 8)
#define BYTE_TO_HW(A, B) ((((uint16_t)(A)) << 8) | (uint8_t)(B))

#define LOBOT_SERVO_FRAME_HEADER         0x55
#define LOBOT_SERVO_ID_WRITE             13
#define LOBOT_SERVO_VIN_READ             27

byte LobotCheckSum(byte buf[])
{
  byte i;
  uint16_t temp = 0;
  for (i = 2; i < buf[3] + 2; i++) {
    temp += buf[i];
  }
  temp = ~temp;
  i = (byte)temp;
  return i;
}
void LobotSerialServoSetID(HardwareSerial &SerialX, uint8_t oldID, uint8_t newID)
{
  byte buf[7];
  buf[0] = buf[1] = LOBOT_SERVO_FRAME_HEADER;
  buf[2] = oldID;
  buf[3] = 4;
  buf[4] = LOBOT_SERVO_ID_WRITE;
  buf[5] = newID;
  buf[6] = LobotCheckSum(buf);
  SerialX.write(buf, 7);
}
int LobotSerialServoReadVin(HardwareSerial &SerialX, uint8_t id)
{
  int count = 10000;
  int ret;
  byte buf[6];

  buf[0] = buf[1] = LOBOT_SERVO_FRAME_HEADER;
  buf[2] = id;
  buf[3] = 3;
  buf[4] = LOBOT_SERVO_VIN_READ;
  buf[5] = LobotCheckSum(buf);

  while (SerialX.available())
    SerialX.read();

  SerialX.write(buf, 6);

  while (!SerialX.available()) {
    count -= 1;
    if (count < 0)
      return -2048;
  }

  if (LobotSerialServoReceiveHandle(SerialX, buf) > 0)
    ret = (int16_t)BYTE_TO_HW(buf[2], buf[1]);
  else
    ret = -2049;

  return ret;
}
int LobotSerialServoReceiveHandle(HardwareSerial &SerialX, byte *ret)
{
  bool frameStarted = false;
  bool receiveFinished = false;
  byte frameCount = 0;
  byte dataCount = 0;
  byte dataLength = 2;
  byte rxBuf;
  byte recvBuf[32];
  byte i;

  while (SerialX.available()) {
    rxBuf = SerialX.read();
    delayMicroseconds(100);
    if (!frameStarted) {
      if (rxBuf == LOBOT_SERVO_FRAME_HEADER) {
        frameCount++;
        if (frameCount == 2) {
          frameCount = 0;
          frameStarted = true;
          dataCount = 1;
        }
      }
      else {
        frameStarted = false;
        dataCount = 0;
        frameCount = 0;
      }
    }
    if (frameStarted) {
      recvBuf[dataCount] = (uint8_t)rxBuf;
      if (dataCount == 3) {
        dataLength = recvBuf[dataCount];
        if (dataLength < 3 || dataCount > 7) {
          dataLength = 2;
          frameStarted = false;
        }
      }
      dataCount++;
      if (dataCount == dataLength + 3) {
        if (LobotCheckSum(recvBuf) == recvBuf[dataCount - 1]) {
          frameStarted = false;
          memcpy(ret, recvBuf + 4, dataLength);
          return 1;
        }
        return -1;
      }
    }
  }
}
HardwareSerial smartMotor(2);

// Change this to the desired ID number
int newID = 2;
void setup() {
  // Begin the Serial Terminal
  Serial.begin(115200);
  Serial.println("Starting....");
  delay(3000);
  Serial.print("Changing ID to ");
  Serial.println(newID);
  // Open the connection to the Smart Motors
  smartMotor.begin(115200, SERIAL_8N1, 16, 17);
  delay(500);
  digitalWrite(32,HIGH);  //Indicator  run indication
  LobotSerialServoSetID(smartMotor, 254, newID); // The first parameter is the serial port which is used for communication. The second parameter is old ID ( the number of old ID is 254 , which is valid for all online servo when you send commands to them) 
                                           //The third parameter is new ID
  delay(500);
  Serial.println("Done");
  Serial.print(LobotSerialServoReadVin(smartMotor,newID));
  Serial.println(" Voltage to Motor to verify changed ID");
}
void loop() {

}
